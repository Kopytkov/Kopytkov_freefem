/* Define geometric*/
real h = 1.1;
real H = 1.7;
real LHuge = 4.5;
real L = 3.1;
real l = 2.0;
real dL = L - l;

border G1(t=0, h) {x = 0; y = H + t;}       /* From G1 to G2 (along x-axis) */
border G2(t=0, L) {x = t; y = H + h;}       /* From G2 to G3 (along y-axis) */
border G3(t=0, H) {x = L; y = H + h - t;}   /* From G3 to G4 (back along x-axis) */
border G4(t=0, LHuge) {x = L + t; y = h;}   /* From G4 to G5 (along y-axis) */
border G5(t=0, h) {x = L + LHuge; y = h - t;}       /* From G5 to G6 (back along x-axis) */
border G6(t=0, (LHuge + dL)) {x = L + LHuge - t; y = 0;}   /* From G6 to G7 (back along y-axis) */
border G7(t=0, H) {x = l; y = t;}   /* From G7 to G8 (along x-axis) */
border G8(t=0, l) {x = l - t; y = H;}   /* Close the contour (along y-axis) */

int n = 30;

/* Create the mesh */
mesh Th = buildmesh(G1(-n) + G2(-n) + G3(-n) + G4(-n) + G5(-n) + G6(-n) + G7(-n) + G8(-n));

/* Visualize the mesh */
plot(Th, cmm = "Mesh of the domain", wait=0);

/* Declaration of variables */
real epsilon = 1e-8;
real rho = 1000;  /* Fluid density (kg/m^3) */
real mu = 0.001;  /* Dynamic viscosity (PaÂ·s) */
real d = 0.1; /* Diameter of tube (m) */
real dt = 0.01;   /* Time step (s) */
real E = 2e11; /* Young's modulus (Pa) */
real delta = 0.007; /* tube thickness (m)*/
real beta = 5.0 * 1e-10; /* Fluid compressibility (Pa^-1) */

real alpha = rho / dt;
int T = 100;  /* Max time (s) */
real Um = 0.0001; /* [m/s] Mean velosity */
func a = 1.0 / sqrt(rho * beta + rho * d / (E*delta)); /* Speed of wave propagation (m/s) */
func vx0 = 6.0*Um*(H+h-y)*(y-H) / (h^2); /* [m/s] Parabolic profil */
func vy0 = 0;
func p0 = 1e5;

/*Differential operators*/
macro grad(u) [dx(u), dy(u)] //EOM  /*EOM = End Of Macros*/
macro div(u1, u2) (dx(u1) + dy(u2)) //EOM

/* Definition of function spaces */
fespace Vh(Th, [P2, P2, P1]);
fespace Vh1(Th, P1); 
fespace Vh2(Th, P2); 

Vh [vx, vy, p], [psi1, psi2, q];

Vh1 pOld = p0;
Vh1 vxOld = vx0, vyOld = 0;

/* Weak formulation of the problem */
solve hydro([vx, vy, p], [psi1, psi2, q]) =
    int2d(Th)(p*q)
    + int2d(Th)(
        - convect([vx0, vy0], -dt, pOld)*q
    )
    + int2d(Th)(
        rho * (a)^2 * div(vx, vy) * q
    )
    + int2d(Th)(alpha*[vx, vy]'*[psi1, psi2])
    + int2d(Th)(
        - alpha*convect([vx0, vy0],-dt,vxOld)*psi1 
        - alpha*convect([vx0, vy0],-dt,vyOld)*psi2
    )
    + int2d(Th)(
        grad(p)'*[psi1, psi2]
        + 32.0 * mu * rho / (d^2) * [vx, vy] '*[psi1, psi2]
    )
    + on(G1, vx=vx0, vy=0, p=p0)
    + on(G2, G4, G6, G8, vx=0)
    + on(G3, G5, G7, vy = 0);

real t = 0;
/* Time-stepping loop to solve the problem */
while(t < T) {
    hydro;
    pOld = p;
    vxOld = vx;
    vyOld = vy;

    /* Visualization */
    plot(vx, cmm = "VelocityX, time = " + t, wait = 0, fill = 1, value = 0, nbiso = 40);
    t += dt;
}
